---
layout: post
title: Plug学习笔记
categories: elixir
tags:
  - phoenix
---

类似于nodejs中的[koa](http://koajs.com/)和中间件, ruby on rails中的[rack](http://rack.github.io/)，plug是一个web中间件规范。 遵循plug规范的模块可以运行在Erlang虚拟机上。 Plug提供了运行这些模块的基础库。


以下是一个最简单的Plug示例。

```elixir
defmodule Server do
  import Plug.Conn

  def init(opts) do
    opts
  end

  def call(conn, _opts) do
    conn
    |> put_resp_content_type("text/plain")
    |> send_resp(200, "Hello world")
  end
end
```

```iex
{:ok, _} = Plug.Adapters.Cowboy.http Server, []
```

运行以上示例需要在你项目中引入相关依赖，[参考](https://hexdocs.pm/plug/readme.html)。


## Plug

共有两种plug。


### 1. function plug

```elixir
def json_header_plug(conn, opts) do
  conn
  |> put_resp_content_type("application/json")
  |> send_resp(200, "Hello world")
end
```

conn是一个`Plug.Conn`结构，通过它可以获得请求相关的信息，通过构造和返回一个新的`Plug.Conn`输出信息。可以使用[Plug.Conn](https://hexdocs.pm/plug/Plug.Conn.html)模块的函数操作此结构。 示例中的 `put_resp_content_type` 和 `send_resp` 就`Plug.Conn`提供的两个函数。


### 2. module plug

模块Plug需要定义两个函数 init 和 call。

```elixir
defmodule JSONHeaderPlug do
  import Plug.Conn

  def init(opts) do
    opts
  end


  def call(conn, _opts) do
    conn |> put_resp_content_type("application/json")
  end
end
```

init的返回值作为call的第二个参数，注意 init 可能会在编译期调用，因此不能返回运行时的对象，比如pid, port 等。


## Plug Router

实践中我们很少直接编写Plug, 而是使用[Plug.Router](https://hexdocs.pm/plug/Plug.Router.html#content)，它会帮我们处理好路径匹配等路由逻辑。

使用 `PlugRouter` 的HelloWorld是这样的:


```elixir
defmodule MyRouter do
  use Plug.Router
  plug Plug.Logger
  plug :match
  plug :dispatch

  get "/hello" do
    conn |> send_resp(200, "helo world")
  end

  match _ do
    conn |> send_resp(404, "oops")
  end
end
```

`Plug.Router`提供了一系统宏用于方便路由的生成， 像示例一样，推荐在路由的最后使用`match`保证未被匹配的路径进入404分支，避免函数抛出异常。

通过`Plug.Router`定义的模块是一个plug，因此可以和其它plug一起组合成更复杂的应用。

`Plug.Router`会把所有路由编译成一个`function`，受益于Erlang虚拟机的高效，底层是使用树结构来支撑查找，因此查找复杂度可以从线性优化到O(log n)。

除了get，还可以使用其他http method定义路由，支持的有 `get`, `post`, `put`, `patch`, `delete`, `options`。 参考[Plug.Router](https://hexdocs.pm/plug/Plug.Router.html)


### 路由和参数

路由中可以指定参数。


```elixir
  get "/products/:name" do
    send_resp(conn, 200, "products #{name}")
  end
```

参数也可以通过 `conn.params["name"]` 和 `conn.path_params["name"]` 取


路径的**最后一部分**支持通配符。


```elixir
  get "/my/*glob" do
    send_resp(conn, 200, "router after #{glob}")
  end
```

```shell
GET http://127.0.0.1:4000/my/book_123
# 输出
router after: book_123


GET http://127.0.0.1:4000/my/book/123
# 输出
router after: book123   # / 注意这里 / 不见了
```

### 解析请求体

如果需要解析`POST`请求，则需要引入 Plug.Parsers 模块。

```elixir
defmodule MyRouter do
  use Plug.Router

  plug Plug.Logger
  plug :match
  plug Plug.Parsers, parsers: [:urlencoded, :json],
                     pass: ["application/x-www-form-urlencoded", "application/json"],
                     json_decoder: Poison
  plug :dispatch

  post "/products" do
    IO.inspect conn.body_params
    conn |> send_resp(200, "success!")
  end
end
```

注意`Plug.Parsers`的位置，需要在 match 之后，dispatch之前， 这样在路由匹配前不会解析请求体，而在执行具体路由函数前请求体已被正常解析，因此可以获得请求体数据。

上述`/products` 支持简单form提交和json数据。

```ssh
curl -d "name=hello&price=12.34" http://127.0.0.1:4000/products -X POST -H "Content-Type: application/x-www-form-urlencoded"
```

```ssh
curl -d '{"name":"hello","price":12.34}' http://127.0.0.1:4000/products -X POST -H "Content-Type: application/json"
```

## 异常处理

Plug提供了两个模块用于处理异常： `Plug.ErrorHandler` 和 `Plug.Debugger` 。

默认情况下，出错对于访问者来说是静敲敲的，可以使用 `Plug.ErrorHandler` 模块自定义异常显示逻辑。

```elixir
defmodule AppRouter do
  use Plug.Router

  if Mix.env == :dev do
    use Plug.Debugger
  end

  use Plug.ErrorHandler

  defp handle_errors(conn, %{kind: _kind, reason: _reason, stack: _stack}) do
    conn |> send_resp(conn.status, "Something went wrong")
  end
end
```

## 路由的编译


每个路由都会被编译成等价的match函数。

```elixir
match "foo/bar", via: :get do
  conn |> send_resp(200, "hello world")
end
```

会被编译成

```elixir
defp match("GET", ["foo", "bar"], conn) do
  ...
end
```

### 文件上传

在mix.exs中添加plug application。

```elixir
  def application do
    [applications: [:logger, :plug]]
  end
```

配置 `Plug.Parsers` 支持文件上传

```elixir
  plug Plug.Parsers, parsers: [:urlencoded, :multipart, :json],
                     pass: ["*/*"],
                     json_decoder: Poison
```

然后就可以在body_params获得上传的文件信息，它是一个[Plug.Upload](https://hexdocs.pm/plug/Plug.Upload.htm)结构

```elixir
  post "/upload" do
    %{filename: filename, path: path} = conn.body_params["file"]
    if File.exists? path do
      dst = Path.join(__DIR__, filename)
      File.cp!(path, dst)
      conn |> send_resp(200, "success!")
    else
      conn |> send_resp(200, "error!")
    end
  end
```

下面是完整的模块示例代码。

```elixir
defmodule MyUpload do
  use Plug.Router

  plug Plug.Logger
  if Mix.env == :dev do
    use Plug.Debugger
  end

  plug :match
  plug Plug.Parsers, parsers: [:urlencoded, :multipart, :json],
                     pass: ["*/*"],
                     json_decoder: Poison
  plug :dispatch

  post "/upload" do
    %{filename: filename, path: path} = conn.body_params["file"]
    if File.exists? path do
      dst = Path.join(__DIR__, filename)
      File.cp!(path, dst)
      conn |> send_resp(200, "success!")
    else
      conn |> send_resp(200, "error!")
    end
  end
end
```

