---
layout: post
title: Elixir Style Guide
categories: elixir
---

参考 [Elixir Guide Style](https://github.com/christopheradams/elixir_style_guide)


## 源代码格式

### 使用两个空格缩进，不使用Tab字符。


```elixir
def some_function do
  do_something
end
```

### 以下位置需要添加空格。

1. 操作符左右
2. 逗号分号和冒号后面
3. 模式匹配不加空格，如大括号和中括号的两端。

```elixir
sum = 1 + 2
{a, b} = {2, 3}
[first | rest] = [1, 2, 3]
Enum.map(["one", <<"two">>, "three"], fn num -> IO.puts num end)
```

### 一元操作符后不要加空格，range操作符左右也不要加空格。

```elixir
0 - 1 == -1
^pinned = some_func()
5 in 1..10
```

### def定义的函数之间使用空行分开，用于表达逻辑的独立性。


### 但是单行函数之间不要有空行。

```elixir
def some_function(nil), do: {:err, "No Value"}
def some_function([]), do: :ok
def some_function([first | rest]) do
  some_function(rest)
end
```

### 如果使用`do:`定义的函数，如果函数体太长，把`do:`另起一行，并且适当缩进。

```elixir
def some_function(args),
  do: Enum.map(args, fn(arg) -> arg <> " is on a very long line!" end)
```


### 如果使用do:定义函数，且有多个函数体，`do:`需要另起一行。

```elixir
# not preferred
def some_function([]), do: :empty
def some_function(_),
  do: :very_long_line_here

# preferred
def some_function([]),
  do: :empty
def some_function(_),
  do: :very_long_line_here
```


### 一行以上的函数体不要使用`do:`定义。


### 使用`|>`连接函数链式调用。

1. 链式调用优先使用 `pipe operator`
2. 多行`pipelines`不缩进
3. `=`右边的`pipelines`需要整体换行且缩进

```elixir
# not preferred
String.strip(String.downcase(some_string))

# preferred
some_string |> String.downcase |> String.strip

# Multiline pipelines are not further indented
some_string
|> String.downcase
|> String.strip

# Multiline pipelines on the right side of a pattern match
# should be indented on a new line
sanitized_string =
  some_string
  |> String.downcase
  |> String.strip
```


### 一个参数的函数调用就不要使用管道操作符了。

```elixir
# not preferred
some_string |> String.downcase

# preferred
String.downcase(some_string)
```


### 使用纯净的变量作为链式调用的开始。

```elixir
# THE WORST!
# This actually parses as String.strip("nope" |> String.downcase).
String.strip "nope" |> String.downcase

# not preferred
String.strip(some_string) |> String.downcase |> String.codepoints

# preferred
some_string |> String.strip |> String.downcase |> String.codepoints
```

## 语法

### 有参数的函数定义需要使用括号，无参数的函数则不需要。

```elixir
# not preferred
def some_function arg1, arg2 do
  # body omitted
end

def some_function() do
  # body omitted
end

# preferred
def some_function(arg1, arg2) do
  # body omitted
end

def some_function do
  # body omitted
end
```

### 多行`if/unless`不使用`do:`。

```elixir
# not preferred
if some_condition, do:
  # a line of code
  # another line of code
  # note no end in this block

# preferred
if some_condition do
  # some
  # lines
  # of code
end
```

### 单行`if/unless`使用`do:`。

```elixir
# preferred
if some_condition, do: # some_stuff
```

### 不要使用`unless`的`else`分支。

```elixir
# not preferred
unless success? do
  IO.puts 'failure'
else
  IO.puts 'success'
end

# preferred
if success? do
  IO.puts 'success'
else
  IO.puts 'failure'
end
```

### 总是用`true`作为`cond`的最后一个分支。

```elixir
cond do
  1 + 2 == 5 ->
    "Nope"
  1 + 3 == 5 ->
    "Uh, uh"
  true ->
    "OK"
end
```

### 函数调用时，函数名和括号之间不要有空格。

```elixir
# not preferred
f (3 + 2) + 1

# preferred
f(3 + 2) + 1
```

### 函数调用时使用括号，特别是在管道操作里。

```elixir
# not preferred
f 3

# preferred
f(3)

# not preferred and parses as rem(2, (3 |> g)), which is not what you want.
2 |> rem 3 |> g

# preferred
2 |> rem(3) |> g
```

### 宏调用时如果有do区块，不需要加括号。

```elixir
# not preferred
quote(do
  foo
end)

# preferred
quote do
  foo
end
```

### 函数调用最后一个参数是函数表达式时，可以有选择地省略括号。

```elixir
# preferred
Enum.reduce(1..10, 0, fn x, acc ->
  x + acc
end)

# also preferred
Enum.reduce 1..10, 0, fn x, acc ->
  x + acc
end
```

### 无参数的函数调用明确使用括号，可以避免和变量冲突。从Elixir 1.4 开始如果没有括号编译器会警告。

```elixir
defp do_stuff, do: ...

# not preferred
def my_func do
  do_stuff # is this a variable or a function call?
end

# preferred
def my_func do
  do_stuff() # this is clearly a function call
end
```

### 连续的with子句需要缩进和对齐，`do:`换行。

```elixir
with {:ok, foo} <- fetch(opts, :foo),
     {:ok, bar} <- fetch(opts, :bar) do
  {:ok, foo, bar}
else
  :error ->
    {:error, :bad_arg}
end
```

## 命名

### 原子，函数和变量使用`snake_case`形式命名。

```elixir
# not preferred
:"some atom"
:SomeAtom
:someAtom

someVar = 5

def someFunction do
  ...
end

def SomeFunction do
  ...
end

# preferred
:some_atom

some_var = 5

def some_function do
  ...
end
```

### 模块使用`CamelCase`形式命名（保持HTTP等术语形式不变）。

```elixir
# not preferred
defmodule Somemodule do
  ...
end

defmodule Some_Module do
  ...
end

defmodule SomeXml do
  ...
end

# preferred
defmodule SomeModule do
  ...
end

defmodule SomeXML do
  ...
end
```

### 断言宏（编译期生成的返回布尔值的函数）且可以在guard子句中使用的需要带`is_`前缀。

``` elixir
defmacro is_cool(var) do
  quote do: unquote(var) == "cool"
end
```

### 不能在guard使用的断言函数需要以`?`结尾。

```elixir
def cool?(var) do
  # Complex check if var is cool not possible in a pure function.
end
```

### 和公共函数同名的私有函数需要以`do_`作为前缀。

```elixir
def sum(list), do: do_sum(list, 0)

# private functions
defp do_sum([], total), do: total
defp do_sum([head | tail], total), do: do_sum(tail, head + total)
```

## 注释

### 书写富有表达力的代码，通过控制流，结构和命名表达程序意图。

### # 前后加空格。

### 注解 `Comment Annotation` 需要写在相关代码的上面。

### 注解关键字后跟冒号和一个空格。

### 使用`TODO`备注后续需要完善的功能。

### 使用`FIXME`备注后续需要修正的代码。

### 使用`OPTIMIZE`备注可能有性能问题的代码。

### 使用`HACK`备注使用不当“技巧”的后续需要重构的代码。

### 使用`REVIEW`备注 需要review确认代码是符合预期工作的。

### 使用其他合适的注释关键字，但需要记录在README中说明


## 模块

### 一个文件一个模块，除非此模块仅在内部被使用。

### 使用`snake_case`作为模块文件名且和`CamelCase`模块名保持一致。

```elixir
# file is called some_module.ex

defmodule SomeModule do
end
```

### 模块名结构和目录结构一致。

### defmodule后面不要加空行。

### 模块代码区块后加一个空行。

### 模块属性和指令以以下顺序排列。

```elixir
@moduledoc
@behaviour
use
import
alias
require
defstruct
@type
@module_attribute
```

以上每个部分之后需要有一个空行隔开。

```elixir
defmodule MyModule do
  @moduledoc """
  An example module
  """

  @behaviour MyBehaviour

  use GenServer

  import Something
  import SomethingElse

  alias My.Long.Module.Name
  alias My.Other.Module.Name

  require Integer

  defstruct name: nil, params: []

  @type params :: [{binary, binary}]

  @module_attribute :foo
  @other_attribute 100

  ...
end
```

### 使用`__MODULE__`变量为引用自己。

```elixir
defmodule SomeProject.SomeModule do
  defstruct [:name]

  def name(%__MODULE__{name: name}), do: name
end
```

### 如果需要一个更好的名称来表达“自己”，可以使用alias。


```elixir
defmodule SomeProject.SomeModule do
  alias __MODULE__, as: SomeModule

  defstruct [:name]

  def name(%SomeModule{name: name}), do: name
end
```

## 文档

### 模块总是包函@moduledoc, 且紧跟着defmoudle。

```elixir
# not preferred

defmodule SomeModule do

  @moduledoc """
  About the module
  """
  ...
end

defmodule AnotherModule do
  use SomeModule
  @moduledoc """
  About the module
  """
  ...
end

# preferred

defmodule SomeModule do
  @moduledoc """
  About the module
  """
  ...
end
```


### 不需要文档的模块明确使用`@moduledoc: false`。

```elixir
defmodule SomeModule do
  @moduledoc false
  ...
end
```

### @moduedoc 区块后加个空行。

```elixir
# not preferred

defmodule SomeModule do
  @moduledoc """
  About the module
  """
  use AnotherModule
end

# preferred
defmodule SomeModule do
  @moduledoc """
  About the module
  """

  use AnotherModule
end
```

### 使用markdonw编写heredoc文档。

```elixir
# not preferred

defmodule SomeModule do
  @moduledoc "About the module"
end

defmodule SomeModule do
  @moduledoc """
  About the module

  Examples:
  iex> SomeModule.some_function
  :result
  """
end

# preferred
defmodule SomeModule do
  @moduledoc """
  About the module

  ## Examples

      iex> SomeModule.some_function
      :result
  """
end
```

## 类型声明

### @typedoc 和 @type 需要在一起定义，且每一部分用空行隔开。

```elixir
defmodule SomeModule do
  @moduledoc false

  @typedoc "The name"
  @type name :: atom

  @typedoc "The result"
  @type result :: {:ok, term} | {:error, term}

  ...
end
```

### 联合类型太长一行放不下时，可使用换行和空格且对齐。

```elixir
# not preferred - no indentation
@type long_union_type :: some_type | another_type | some_other_type
| a_final_type

# preferred
@type long_union_type :: some_type | another_type | some_other_type
                       | a_final_type

# also preferred - one return type per line
@type long_union_type :: some_type
                       | another_type
                       | some_other_type
                       | a_final_type
```

### 主类型命名为`t`。

```elixir
defstruct name: nil, params: []

@type t :: %__MODULE__{
  name: String.t,
  params: Keyword.t
}
```

### 函数声明需要编写在函数前，且之间不要有空行。

```elixir
@spec some_function(term) :: result
def some_function(some_data) do
  {:ok, some_data}
end
```

## 结构

### 如果结构体所有属性默认值都为nil，使用原子列表定义。

```elixir
# not preferred
defstruct name: nil, params: nil

# preferred
defstruct [:name, :params]
```

### 多行结构体其他行和首个key对齐。

```elixir
defstruct foo: "test", bar: true, baz: false,
          qux: false, quux: nil
```

## 异常

### 异常名称以`Error`结尾。

### 异常message使用小写英文单词，且最后不加标点。

```elixir
# not preferred
raise ArgumentError, "This is not valid."

# preferred
raise ArgumentError, "this is not valid"
```

## 集合

等待添加


## 字符串

使用连接符`<>`匹配字符串，而不使用二进制模式匹配字符串。


```elixir
# not preferred
<<"my"::utf8, _rest>> = "my string"

# preferred
"my" <> _rest = "my string"
```

## 正则表达式

等待添加


## 元编程

### 避免不必要的元编程


## 测试

### assertion语句中保持实际值和期望值位置一致，优先将期望值放在右侧，除非是模式匹配场景。

```elixir
# preferred - expected result on the right
assert actual_function(1) == true
assert actual_function(2) == false

# not preferred - inconsistent order
assert actual_function(1) == true
assert false == actual_function(2)

# required - the assertion is a pattern match
assert {:ok, expected} = actual_function(3)
```

## 其他建议

这些建议在社区中尚未流行，但可能有价值。

### cond

```elixir
cond do
  1 + 2 == 5 ->
    "Nope"
  1 + 3 == 5 ->
    "Uh, uh"
  :else ->
    "OK"
end

# is the same as
cond do
  1 + 2 == 5 ->
    "Nope"
  1 + 3 == 5 ->
    "Uh, uh"
  true ->
    "OK"
end
```


## 其他风格指南

- [Aleksei Magusev's Elixir Style Guide ](https://github.com/lexmag/elixir-style-guide#readme)
- [Credo's Elixir Style Guide](https://github.com/rrrene/elixir-style-guide#readme)


## 工具

[code-analysis] https://github.com/h4cc/awesome-elixir#code-analysis
