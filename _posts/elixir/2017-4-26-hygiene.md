---
layout: post
title: 宏的卫生问题
categories: elixir
---

宏是一种将代码注入到调用端的技术，使用这种技术可以完成一些常规流程之外的任务，比如可以自定义控制流程等。  

Elixir中的宏是卫生的（官方说话是hygiene），我是第一次在编程的语境中看到这个词，觉得很不可思议。其背后的思想是：宏注入的代码应该是清洁和无污染的。  

这种设计一方面避免不小心造成的名字冲突，另一方面迫使我们明确地进入或使用调用者的上下文，这和Elixir总体的设计理念是一致的，清晰、明确、无歧义，为此可以牺牲一些美观，允许存在一些无害的重复。

以下示例演示了宏中定义的变量对于调用端是不可见的。


```elixir
defmodule Hygiene do
  defmacro no_interference do
    quote do
      a = 1
    end
  end
end


defmodule HygieneTest do
  require Hygiene
  def go do
    a = 13
    Hygiene.no_interference
    a
  end
end


IO.puts HygieneTest.go
# 13
```

以上示例中，宏中的定义的`a`变量并不会影响到调用端的代码，即两个`a`是不同的。

我们尝试把它展开看看。

```elixir
defmodule Mex do
  def expand_all(n, env) do
    Macro.prewalk(n, &Macro.expand(&1, env))
  end

  defmacro mex(do: block) do
    block
    |> expand_all(__CALLER__)
    |> IO.inspect
  end
end


defmodule Main do
  require Mex
  require Hygiene

  def run do
    Mex.mex do
      a = 13
      Hygiene.no_interference
      a
    end
  end
end


Main.run
```

其中`Mex`模块用于把包含宏的代码片断（抽象语法树）递归展开, 运行结果如下：

```elixir
{:__block__, [line: 27],
 [{:=, [line: 29], [{:a, [line: 29], nil}, 13]},
  {:=, [], [{:a, [counter: -576460752303423453], Hygiene}, 1]},
  {:a, [line: 31], nil}]}
```

以上有三处涉及到`a`：

```elixir
{:a, [line: 29], nil}
{:a, [counter: -576460752303423453], Hygiene}
{:a, [line: 31], nil}
```

中间这句是由宏注入的，会有不同的上下文，所以和调用端的变量`a`互不干扰。


但Elixir基础库也提供了[var!](https://hexdocs.pm/elixir/Kernel.html#var!/2)用于在宏中定义或设置调用端相应变量的值。

```elixir
defmodule Hygiene do
  defmacro interference do
    quote do
      var!(a) = 1
    end
  end
end

defmodule HygieneTest do
  def go do
    require Hygiene
    a = 13
    Hygiene.interference
    a
  end
end

IO.puts HygieneTest.go
```

运行以上示例会输出

```elixir
warning: variable "a" is unused
  e.exs:12

1
```

可以看到输出的值是宏中定义的`a`，但同时也给了个警告，告诉我们第一个`a`被定义了但没有使用过。

同样将宏代码展开：

```elixir
defmodule Main do
  require Mex
  require Hygiene

  def run do
    Mex.mex do
      a = 13
      Hygiene.interference
      a
    end
  end
end

Main.run
```

输出结果为：

```elixir
{:__block__, [line: 37],
 [{:=, [line: 39], [{:a, [line: 39], nil}, 13]},
  {:=, [], [{:a, [var: true], nil}, 1]}, {:a, [line: 41], nil}]}
```

现在`context`都是`nil`，所以会影响到调用方的上下文，慎用！！

宏可以注入代码，可以注入`import`，`alias`等代码。


```elixir
defmodule A.B.C do
  def hi do
    2
  end
end


defmodule Complex do
  def add(a, b) do
    a + b
  end

  def mul(a, b) do
    a * b
  end

  defmacro __using__(_opts) do
    import A.B.C              # 只在当前词法作用域内生效，不会注入到目标模块
    quote do
      import Complex          # 在目标模块注入import Complex代码
      alias A.B.C, as: ABC    # alias代码也可以注入
      def say do              # 注入一个函数
        hi() + 1
      end
    end
  end
end


defmodule UseComplex do
  use Complex

  def go do
    IO.puts add(1, 2)     # 来自于Complex，通过注入的import Complex语句导入
    IO.puts mul(3, 4)
    IO.puts say()         # 来自于宏
    IO.puts ABC.hi()      # alias A.B.C, as: ABC
    #IO.puts hi()    # (CompileError) c.exs:38: undefined function hi/0
  end
end


UseComplex.go
```
